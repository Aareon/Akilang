class Player {
    x:i32
    y:i32
}

class Robot{
    x:i32
    y:i32
}

const {
    WIDTH=40,
    HEIGHT=20,
    FIELD_SIZE = (WIDTH+1)*HEIGHT
}

uni {
    field:byte[WIDTH,HEIGHT],
    #display:byte[(WIDTH+1)*HEIGHT], # can't do this yet
    display:byte[FIELD_SIZE],
    player:Player,
    robots:Robot[10]
}

def show(){
    var z=0
    loop(y=0, y<HEIGHT){
        loop(x=0, x<WIDTH){
            display[z]=(
                if field[x,y]==0B
                    then 32B
                    else field[x,y]
            )
            z=z+1
        }
        display[z]=10B
        z=z+1
    }

    display[z]=0B

    loop (x=0, x<9){
        display[(robots[x].y*(WIDTH+1))+robots[x].x]=72B
    }

    display[(player.y*(WIDTH+1))+player.x]=232B

    print(c_array_ptr(display))
}

def setup(){

    var nx=0, ny=0

    loop(y=0, y<HEIGHT)
        loop(x=0, x<WIDTH)
            field[x,y]=0B

    loop (x=0, x<HEIGHT){
        field[0,x]=219B
        field[WIDTH-1,x]=219B
    }
    loop (x=0, x<WIDTH){
        field[x,0]=219B
        field[x,HEIGHT-1]=219B
    }

    #Place electrodes

    loop (x=0, x<20){
        loop {
            nx = rnd(WIDTH-4)+2
            ny = rnd(HEIGHT-4)+2
            if field[nx,ny]==0B then break
        }
        field[nx,ny]=177B
    }

    # Place robots    
    loop (x=0, x<9) {
        loop {
            nx = rnd(WIDTH-4)+2
            ny = rnd(HEIGHT-4)+2
            if field[nx,ny]==0B then break
        }
        robots[x].x=nx
        robots[x].y=ny
    }    

    # Place human

    player.x=20
    player.y=10

    0
}

def main(){
    loop {
        setup()
        var x:i32
        loop {
            show()
            x=inkey()
            match x {
                72: player.y=player.y-1
                80: player.y=player.y+1
                75: player.x=player.x-1
                77: player.x=player.x+1
                113, 81: break
                default: continue
            }
            if field[player.x,player.y]==177B then {
                print ("FROTZZZZZ\nYou just stepped into an electrified fence.")
                break
            }
        }
        loop {
            print ("Play again? [y/n] > ")
            x=inkey()
            match x {
                121: {x=1 break}
                110: {x=0 break}
                default: continue
            }
        }
        if x!=1 then
        {
            print ("Goodbye!")
            break
        }
    }
    return 0
}